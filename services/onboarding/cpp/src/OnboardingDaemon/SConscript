# Copyright (c) 2013-2014, AllSeen Alliance. All rights reserved.
#
#    Contributed by Qualcomm Connected Experiences, Inc.,
#    with authorization from the AllSeen Alliance, Inc.
#    
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#    
#        http://www.apache.org/licenses/LICENSE-2.0
#    
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#    
#    Pursuant to Section 1 of the License, the work of authorship constituting
#    a Work and any Contribution incorporated in the Work shall mean only that
#    Contributor's code submissions authored by that Contributor.  Any rights
#    granted under the License are conditioned upon acceptance of these
#    clarifications.

Import('onb_env')

local_env = onb_env.Clone()

vars = Variables()
vars.Add('JSON_LIB', 'The JSON library to link against.', 'json')
vars.Update(local_env)
Help(vars.GenerateHelpText(local_env))

if local_env['OS'] == 'openwrt':
   local_env.Append(CPPDEFINES = '_OPEN_WRT_')

local_env.Append(CPPPATH = ['.'])

local_env.Prepend(LIBS = [ 'alljoyn_onboarding',
                           'alljoyn_config',
                           'alljoyn_about',
                           'alljoyn_services_common',
                           local_env['JSON_LIB'] ])

if local_env['BR'] == 'on' and local_env.has_key('brobj') and local_env.has_key('ajrlib'):
    # Build apps with bundled daemon support
    local_env.Prepend(LIBS = [local_env['brobj'], local_env['ajrlib']])

# copy the wifi_scan_results to the dist for easier debug
local_env.Install('$ONBOARDING_DISTDIR/bin', 'wifi_scan_results')

local_env.Append(CPPPATH = local_env.Dir('$APP_COMMON_DIR/cpp/samples_common').srcnode())
local_env.VariantDir('AppCommon', '$APP_COMMON_DIR/cpp/samples_common/', duplicate = 0)
cobjs = local_env.SConscript('AppCommon/SConscript', {'env': local_env})

srcs = local_env.Glob('*.cc')
objs = local_env.Object(srcs)
objs.extend(cobjs)

local_env.Install('$ONBOARDING_DISTDIR/bin', 'FactoryOnboardingService.conf')
local_env.Install('$ONBOARDING_DISTDIR/bin', 'OnboardingService.conf')
prog = local_env.Program('onboarding-daemon', objs)

Return('prog')
