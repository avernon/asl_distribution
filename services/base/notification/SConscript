# Copyright AllSeen Alliance. All rights reserved.
#
#    Contributed by Qualcomm Connected Experiences, Inc.,
#    with authorization from the AllSeen Alliance, Inc.
#    
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#    
#        http://www.apache.org/licenses/LICENSE-2.0
#    
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#    
#    Pursuant to Section 1 of the License, the work of authorship constituting
#    a Work and any Contribution incorporated in the Work shall mean only that
#    Contributor's code submissions authored by that Contributor.  Any rights
#    granted under the License are conditioned upon acceptance of these
#    clarifications.

import os

Import('env')

env['_ALLJOYN_NOTIFICATION_'] = True
env['OBJDIR_SERVICES_NOTIFICATION'] = env['OBJDIR'] + '/services/notification'

# Make config library and header file paths available to the global environment
env.Append(LIBPATH = '$DISTDIR/notification/lib');
env.Append(CPPPATH = '$DISTDIR/notification/inc');

if not env.has_key('_ALLJOYN_ABOUT_') and os.path.exists('../../../core/alljoyn/services/about/SConscript'):
    env.SConscript('../../../core/alljoyn/services/about/SConscript')

if not env.has_key('_ALLJOYN_SERVICES_COMMON_') and os.path.exists('../services_common/SConscript'):
    env.SConscript('../services_common/SConscript')

if 'cpp' in env['bindings'] and not env.has_key('_ALLJOYNCORE_') and os.path.exists('../../../core/alljoyn/alljoyn_core/SConscript'):
    env.SConscript('../../../core/alljoyn/alljoyn_core/SConscript')

if 'java' in env['bindings'] and not env.has_key('_ALLJOYN_JAVA_') and os.path.exists('../../../core/alljoyn/alljoyn_java/SConscript'):
    env.SConscript('../../../core/alljoyn/alljoyn_java/SConscript')

nsenv = env.Clone()

# ASABASE-452, ASACORE-1419
# Even though we have deprecated the About Service code the notification service is
# designed so a developer can use the deprecated AboutService or the new
# About Feature code. So the notification service can continue to support the
# deprecated methods we must turn off the deprecated-declarations warning.
if nsenv['OS_GROUP'] == 'posix':
    nsenv.Append(CXXFLAGS = ['-Wno-deprecated-declarations'])

for b in nsenv['bindings']:
    if os.path.exists('%s/SConscript' % b):
        nsenv.VariantDir('$OBJDIR_SERVICES_NOTIFICATION/%s' % b, b, duplicate = 0)

nsenv.SConscript(['$OBJDIR_SERVICES_NOTIFICATION/%s/SConscript' % b for b in env['bindings'] if os.path.exists('%s/SConscript' % b) ],
                 exports = ['nsenv'])
