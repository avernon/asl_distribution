# Copyright AllSeen Alliance. All rights reserved.
#
#    Contributed by Qualcomm Connected Experiences, Inc.,
#    with authorization from the AllSeen Alliance, Inc.
#    
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#    
#        http://www.apache.org/licenses/LICENSE-2.0
#    
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#    
#    Pursuant to Section 1 of the License, the work of authorship constituting
#    a Work and any Contribution incorporated in the Work shall mean only that
#    Contributor's code submissions authored by that Contributor.  Any rights
#    granted under the License are conditioned upon acceptance of these
#    clarifications.

Import('env')
import os

services_common_root_default = '../../../services_common'

vars = Variables()
if not env.has_key('SERVICES_COMMON_ROOT'):
    vars.Add('SERVICES_COMMON_ROOT', 'Root directory of Services Common', services_common_root_default)
vars.Update(env)

# if SERVICES_COMMON_ROOT passed in via the env., use it:
if env.has_key('SERVICES_COMMON_ROOT_FROM_COMPONENT'):
    env.Replace(SERVICES_COMMON_ROOT = env['SERVICES_COMMON_ROOT_FROM_COMPONENT'])
Help(vars.GenerateHelpText(env))

cps_env = env.Clone()

cps_env['CONTROLPANEL_DEFINE_STRING'] = 'CONTROLPANEL_SERVICE'
cps_env.Append(CPPDEFINES = '$CONTROLPANEL_DEFINE_STRING')
if cps_env['VARIANT'] == 'debug':
    cps_env.Append(CPPDEFINES = ['ER_DEBUG_AJSVCAPP=1'])
    cps_env.Append(CPPDEFINES = ['ER_DEBUG_AJSVCALL=1'])
    cps_env.Append(CPPDEFINES = ['AJ_DEBUG_RESTRICT=AJ_DEBUG_INFO'])


target_name = 'ControlPanelClient'
source_files = [
    'ControlPanelClientGenerated.c',
    'ControlPanelClient.c',
    'PropertyStore.c',
]

cps_env.Append(CPPPATH = [
                            cps_env.Dir('.'),
                            Dir('../../inc'),
                         ])

objs = cps_env.Object(source_files)
cps_obj = cps_env.SConscript('../../src/CPSControllee/SConscript', {'cps_env': cps_env })

services_common_objs = cps_env.SConscript('$SERVICES_COMMON_ROOT/SConscript', {'services_common_env': cps_env})
objs.extend(services_common_objs)

cps_env.Program(
            target = target_name, 
            source = [
                objs,
                cps_env['aj_obj'],
                cps_obj
            ])

