# Copyright AllSeen Alliance. All rights reserved.
#
#    Contributed by Qualcomm Connected Experiences, Inc.,
#    with authorization from the AllSeen Alliance, Inc.
#    
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#    
#        http://www.apache.org/licenses/LICENSE-2.0
#    
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#    
#    Pursuant to Section 1 of the License, the work of authorship constituting
#    a Work and any Contribution incorporated in the Work shall mean only that
#    Contributor's code submissions authored by that Contributor.  Any rights
#    granted under the License are conditioned upon acceptance of these
#    clarifications.
# 

Import('env')

# AllJoyn Sources
srcs = env.Glob('*.cc')

objs = env.Object(srcs)
objs_shared = env.SharedObject(srcs)

# Add the export DEF file for Windows.
if env['OS_GROUP'] == 'windows':
    objs_shared.append(env.Dir('.').abspath + '/${OS_GROUP}/alljoyn_c.def')

# Windows produces two types of .lib files.  
#    - static library used for statically linking 
#    - import library used for linking with a shared library (i.e. a dll).
# Depending on the build setup both file types may be needed.  To distinguish 
# between the static library and the import library on windows the static 
# library will be called alljoyn_c_static.lib and the import library will be 
# called alljoyn_c.lib.
# see http://msdn.microsoft.com/en-us/library/ba1z7822.aspx for more info.
# For all other OSs there is no need to make this distinction.   
if env['OS_GROUP'] == 'windows':
    liballjoyn_c_static = env.StaticLibrary('alljoyn_c_static', objs)
else:
    liballjoyn_c_static = env.StaticLibrary('alljoyn_c', objs)
liballjoyn_c_shared = env.SharedLibrary('alljoyn_c', objs_shared)

Return('liballjoyn_c_static', 'liballjoyn_c_shared', 'objs_shared')
