# Copyright AllSeen Alliance. All rights reserved.
#
#    Contributed by Qualcomm Connected Experiences, Inc.,
#    with authorization from the AllSeen Alliance, Inc.
#    
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#    
#        http://www.apache.org/licenses/LICENSE-2.0
#    
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#    
#    Pursuant to Section 1 of the License, the work of authorship constituting
#    a Work and any Contribution incorporated in the Work shall mean only that
#    Contributor's code submissions authored by that Contributor.  Any rights
#    granted under the License are conditioned upon acceptance of these
#    clarifications.
#

Import('gateway_env')

gwcnc_env = gateway_env.Clone()
gwcnc_env.Append(CPPPATH = gwcnc_env.Dir('$APP_COMMON_DIR/cpp/samples_common').srcnode())
gwcnc_env.Prepend(LIBS = ['alljoyn_about', 'alljoyn_services_common', 'alljoyn_notification', 'alljoyn_config', 'alljoyn_gwconnector'])

srcs = gwcnc_env.Glob('*.cc')
objs = gwcnc_env.Object(srcs)

gwcnc_env.VariantDir('AppCommon', '$APP_COMMON_DIR/cpp/samples_common/', duplicate = 0)
cobjs = gwcnc_env.SConscript('AppCommon/SConscript', {'env': gwcnc_env})
objs.extend(cobjs)

prog = gwcnc_env.Program('alljoyn-gwconnectorsample', objs)

# Prepare for building tar
gwcnc_env.Install('$GWCNC_DISTDIR/tar', gwcnc_env.Glob('Manifest.xml'))
gwcnc_env.Install('$GWCNC_DISTDIR/tar/bin', prog)
gwcnc_env.Install('$GWCNC_DISTDIR/tar/bin', gwcnc_env.Glob('postTweet.sh'))
gwcnc_env.Install('$GWCNC_DISTDIR/tar/bin', gwcnc_env.Glob('directMessage.sh'))

if gwcnc_env['OS'] == 'linux':
    gwcnc_env.Install('$GWCNC_DISTDIR/tar/lib', gwcnc_env.Glob('$GWCNC_DISTDIR/lib/*.so'))
    gwcnc_env.Install('$GWCNC_DISTDIR/tar/lib', gwcnc_env.Glob('$DISTDIR/cpp/lib/*.so'))
    gwcnc_env.Install('$GWCNC_DISTDIR/tar/lib', gwcnc_env.Glob('$DISTDIR/about/lib/*.so'))
    gwcnc_env.Install('$GWCNC_DISTDIR/tar/lib', gwcnc_env.Glob('$DISTDIR/services_common/lib/*'))
    gwcnc_env.Install('$GWCNC_DISTDIR/tar/lib', gwcnc_env.Glob('$DISTDIR/notification/lib/*'))
    gwcnc_env.Install('$GWCNC_DISTDIR/tar/lib', gwcnc_env.Glob('$DISTDIR/config/lib/*.so'))

Return('prog')
